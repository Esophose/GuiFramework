import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'dev.rosewood'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()

    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    disableAutoTargetJvm()
}

dependencies {
    // GuiFramework
    implementation project(':Framework')
    implementation project(':API')

    // Spigot
    compileOnly "org.spigotmc:spigot-api:$spigotVersion-R0.1-SNAPSHOT"
}

jar {
    enabled = false
}

shadowJar {
    archiveClassifier.set(null)
    archiveBaseName.set('GuiFrameworkExamples')
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

build.dependsOn shadowJar
