plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'maven-publish'
    id 'java'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'
    group = 'dev.rosewood'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(21))
        disableAutoTargetJvm()
    }

    repositories {
        mavenCentral()

        maven { url = 'https://repo.rosewooddev.io/repository/public' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.1.0'
    }
}

dependencies {
    implementation project(':Framework')
    implementation project(':API')
    implementation project(':InventoryView:Wrapper')
    implementation project(':InventoryView:New')
    implementation project(':InventoryView:Old')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'guiframework'
            from components.java
            pom {
                name = 'guiframework'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
