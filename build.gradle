plugins {
    id 'maven-publish'
}

allprojects {
    apply plugin: 'java'

    group = 'dev.esophose'
    version = '1.1.0'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()

        maven { url = 'https://repo.codemc.org/repository/nms' }
    }

    dependencies {
        // Spigot
        compileOnly 'org.spigotmc:spigot:1.15.2-R0.1-SNAPSHOT'

        // JetBrains Annotations
        compileOnly 'org.jetbrains:annotations:13.0'
    }
}

configurations {
    childJars
}

dependencies {
    compileOnly project(':API')
    compileOnly project(':Framework')

    subprojects.each {
        childJars project(it.path)
    }
}

jar {
    dependsOn configurations.childJars
    from {
        configurations.childJars.collect {
            zipTree(it)
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'guiframework'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'guiframework'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUsername') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUsername
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.codemc.org/repository/maven-releases/'
                def snapshotsRepoUrl = 'https://repo.codemc.org/repository/maven-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
